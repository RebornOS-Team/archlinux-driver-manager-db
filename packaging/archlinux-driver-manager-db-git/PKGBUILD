#! /usr/bin/env sh

# Maintainer: shivanandvp <shivanandvp@rebornos.org>

pkgver=0.0.1
pkgrel=1

PACKAGE_NAME_STUB="archlinux-driver-manager-db"
pkgname="$PACKAGE_NAME_STUB-git"
provides=("$PACKAGE_NAME_STUB")
conflicts=("$PACKAGE_NAME_STUB" "$PACKAGE_NAME_STUB-local")
depends=()
makedepends=('git' 'archlinux-driver-manager')
source=("git+https://github.com/RebornOS-Developers/$PACKAGE_NAME_STUB") 
sha256sums=('SKIP')
PROJECT_DIRECTORY="${srcdir}/$PACKAGE_NAME_STUB"
RESOURCE_DIRECTORY="$PROJECT_DIRECTORY/packaging/resources"
BUILD_DIRECTORY="$PROJECT_DIRECTORY/target"

arch=('x86_64')
pkgdesc='Database for Arch Linux Driver Manager.'
url="https://github.com/RebornOS-Developers/$PACKAGE_NAME_STUB"
license=('MPL-2.0')

pkgver() {
    (   cd "${srcdir}/$PACKAGE_NAME_STUB"
        set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    :
}

build() {     
    cd "$PROJECT_DIRECTORY"
    archlinux-driver-manager generate-database human_readable_database.yaml database.ron
}

package() {
    # To bypass makepkg's dependency check during build-time
    optdepends+=("archlinux-driver-manager")

    cd "$PROJECT_DIRECTORY"
    install -Dm644 "database.ron" "$pkgdir/var/lib/archlinux-driver-manager/database.ron"
}
